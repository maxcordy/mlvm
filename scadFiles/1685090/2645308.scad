

/* [Enclosure] */

// Select type of enclosure
boxType = "hammond1590b"; // [hammond1550a: Hammond 1550A, hammond1550b: Hammond 1550B,hammond1550p: Hammond 1550P,hammond1550q: Hammond 1550Q, hammond1590a: Hammond 1590A, hammond1590b: Hammond 1590B, hammond1590g: Hammond 1590G]

// Select color of the enclosure
boxColor = "gray"; // [gray, silver, black, blue, green, lightgray, orange, purple, red, yellow]

/* [510 connector] */

// Select your 510 connector
type_510_connector = "varitube22mmV2Slotted"; // [fdv15.5mmV4: Fat Daddy Vapes V4 15.5mm, fdv22mmV4: Fat Daddy Vapes V4 22mm, fdv30mmV4: Fat Daddy Vapes V4 30mm, fdv15.5mmV4lp: Fat Daddy Vapes V4 15.5mm Low Profile, fdv22mmV4lp: Fat Daddy Vapes V4 22mm Low Profile, fdv22mmV4lps: Fat Daddy Vapes V4 22mm Low Profile Short, varitube12mmBottomFeed: Varitube 12mm Bottom Feed, varitube14mm: Varitube 14mm, varitube22mmBottomFeed: Varitube 22mm Bottom Feed, varitube22mmV2Flat: Varitube 22mm Flat V2, varitube22mmV2Slotted: Varitube 22mm Slotted V2, varitube28.5mm: Varitube 28.5mm]
// Select color of the 510 connector
color_510_connector = "silver"; // [gray, silver, black]

// x offset from the center
xOffset_510 = 0; // [-100:20:100]
// y offset from the center
yOffset_510 = 0; // [-100:0.1:100]
// z offset from the bottom
zOffset_510 = 112.4; // [-200:0.1:200]

// x rotation
xRotate_510 = 0; // [0:360]
// y rotation
yRotate_510 = 0; // [0:360]
// z rotation
zRotate_510 = 0; // [0:360]

/* [Fire Switch] */

// Enable a fire switch?
enableFireSwitch = "yes"; // [yes, no]
// Select type of fire switch
fireSwitchType = "cloneTec12mmRaised"; // [cloneTec12mmFlat: CloneTec 12mm Flat, cloneTec12mmRaised: CloneTec 12mm Raised, vandal16mmFlat: Vandal Proof 16mm Flat, vandal16mmRaised: Vandal Proof 16mm Raised, vandal19mmFlat: Vandal Proof 19mm Flat, vandal19mmRaised: Vandal Proof 19mm Raised]
// Select color of fire switch
fireSwitchColor = "gray"; // [gray: Gray, silver: Silver, black: Black]

// x offset from the center
xOffsetFireSwitch = 30.3; // [-100:0.1:100]
// y offset from the center
yOffsetFireSwitch = 0; // [-100:0.1:100]
// z offset from the bottom
zOffsetFireSwitch = 90; // [-200:0.1:200]

// x rotation
xRotateFireSwitch = 0; // [0:360]
// y rotation
yRotateFireSwitch = 90; // [0:360]
// z rotation
zRotateFireSwitch = 0; // [0:360]

/* [Volt Meter] */

// Enable the volt meter?
enableVoltMeter = "yes"; // [yes, no]
// Select your volt meter type
voltMeterType = "seedVoltMeterGreen"; // [seedVoltMeterGreen: Seed Volt Meter Green, seedVoltMeteryellow: Seed Volt Meter Yellow, seedVoltMeterRed: Seed Volt Meter Red, seedVoltMeterBlue: Seed Volt Meter Blue]

// x offset from the center
xOffsetVoltMeter = 0; // [-100:0.1:100]
// y offset from the center
yOffsetVoltMeter = 6.7; // [-100:0.1:100]
// z offset from the bottom
zOffsetVoltMeter = 90; // [-200:0.1:200]

// x rotation
xRotateVoltMeter = 270; // [0:360]
// y rotation
yRotateVoltMeter = 180; // [0:360]
// z rotation
zRotateVoltMeter = 0; // [0:360]

/* [Sled] */

// Enable a battery sled?
enableSled = "yes"; // [yes, no]
// Select your volt meter type
sledType = "keystone1048"; // [keystone1042: Keystone single 18650 (1042), keystone1042empty: Keystone single 18650 empty (1042), keystone1048: Keystone dual 18650 (1048), keystone1048empty: Keystone dual 18650 empty (1048), keystone1048left: Keystone dual 18650 left (1048), keystone1048right: Keystone dual 18650 right (1048)]

// x offset from the center
xOffsetSled = 0; // [-100:0.1:100]
// y offset from the center
yOffsetSled = 13.5; // [-100:0.1:100]
// z offset from the bottom
zOffsetSled = 40.7; // [-200:0.1:200]

// x rotation
xRotateSled = 90; // [0:360]
// y rotation
yRotateSled = 90; // [0:360]
// z rotation
zRotateSled = 0; // [0:360]

/* [PCB] */

// Enable a pcb?
enablePcb = "yes"; // [yes, no]
// Select PCB
pcbType = "raptor10a"; // [raptor10a: Naos Raptor 10A, raptor20a: Naos Raptor 20A]

// x offset from the center
xOffsetPcb = -21.4; // [-100:0.1:100]
// y offset from the center
yOffsetPcb = 0; // [-100:0.1:100]
// z offset from the bottom
zOffsetPcb = 90; // [-200:0.1:200]

// x rotation
xRotatePcb = 90; // [0:360]
// y rotation
yRotatePcb = 0; // [0:360]
// z rotation
zRotatePcb = 270; // [0:360]

/* [Squonker] */

// Enable squonker?
enableSquonk = "no"; // [yes, no]
// Select Bottle type
squonkType = "fdv18mm"; // [fdv18mm: Fat Daddy Vapes 18mm round, raptor20a: Naos Raptor 20A]

// x offset from the center
xOffsetSquonk = -50; // [-100:0.1:100]
// y offset from the center
yOffsetSquonk = 0; // [-100:0.1:100]
// z offset from the bottom
zOffsetSquonk = 0; // [-200:0.1:200]

// x rotation
xRotateSquonk = 0; // [0:360]
// y rotation
yRotateSquonk = 0; // [0:360]
// z rotation
zRotateSquonk = 0; // [0:360]

// height
squonkCutoutHeight = 45; // [1:200]
// width
squonkCutoutWidth = 23; // [1:200]
// depth
squonkCutoutDepth = 5; // [1:200]

// x offset from the center
xOffsetSquonkCutout = 10; // [-100:0.1:100]
// y offset from the center
yOffsetSquonkCutout = -13.5; // [-100:0.1:100]
// z offset from the bottom
zOffsetSquonkCutout = 30; // [-200:0.1:200]

// x rotation
xRotateSquonkCutout = 0; // [0:360]
// y rotation
yRotateSquonkCutout = 0; // [0:360]
// z rotation
zRotateSquonkCutout = 0; // [0:360]

/* [Passthrough] */

// Enable passthrough?
enablePassthrough = "no"; // [yes, no]
// Select passthrough type
passthroughType = "hole"; // [hole: Hole, jack608: Switchcraft Unmated Power Jack]

// Passthrough hole diameter
passthroughDiameter = 5;
passthroughDepth = 6;

// x offset from the center
xOffsetPassthrough = -29.5; // [-100:0.1:100]
// y offset from the center
yOffsetPassthrough = 0; // [-100:0.1:100]
// z offset from the bottom
zOffsetPassthrough = 15; // [-200:0.1:200]

// x rotation
xRotatePassthrough = 90; // [0:360]
// y rotation
yRotatePassthrough = 90; // [0:360]
// z rotation
zRotatePassthrough = 0; // [0:360]

/* [View Mode] */

// If visualisation is enabled, the parts are rendered
visualize = "yes"; // [yes, no]
// Which parts of the box to display
part = "both"; // [both, body, lid]

/* [Hidden] */
sledColor = "black";
voltMeterColor = "white";

// Resolution for roundness: higher -> rounder edges -> longer render time
$fn = 36;

pos510 = [xOffset_510, yOffset_510, zOffset_510];
posFireSwitch = [xOffsetFireSwitch, yOffsetFireSwitch, zOffsetFireSwitch];
posVoltMeter = [xOffsetVoltMeter, yOffsetVoltMeter, zOffsetVoltMeter];
posSled = [xOffsetSled, yOffsetSled, zOffsetSled];
posPcb = [xOffsetPcb, yOffsetPcb, zOffsetPcb];
posSquonk = [xOffsetSquonk, yOffsetSquonk, zOffsetSquonk];
posSquonkCutout = [xOffsetSquonkCutout, yOffsetSquonkCutout, zOffsetSquonkCutout];
posPassthrough = [xOffsetPassthrough, yOffsetPassthrough, zOffsetPassthrough];

rot510 = [xRotate_510, yRotate_510, zRotate_510];
rotFireSwitch = [xRotateFireSwitch, yRotateFireSwitch, zRotateFireSwitch];
rotVoltMeter = [xRotateVoltMeter, yRotateVoltMeter, zRotateVoltMeter];
rotSled = [xRotateSled, yRotateSled, zRotateSled];
rotPcb = [xRotatePcb, yRotatePcb, zRotatePcb];
rotSquonk = [xRotateSquonk, yRotateSquonk, zRotateSquonk];
rotSquonkCutout = [xRotateSquonkCutout, yRotateSquonkCutout, zRotateSquonkCutout];
rotPassthrough = [xRotatePassthrough, yRotatePassthrough, zRotatePassthrough];

/**
 * To activate animation:
 * View -> Animated
 * Set FPS to 15 and steps to 100
 */
xRotate = (part != "both") ? 270 : 0;
yRotate = (part == "lid") ? 180 : 0;
zRotate = $t * 360;

rotate([xRotate, yRotate, zRotate]) {
  renderFull();
  if(visualize == "yes" && part == "both")
    visualisation();
}/**
 * Available 510 connectors:
 * 
 * Varitube:
 * 
 * + http://www.varitube.com/VT-510-V2-Connector--Self-adjusting-Center-Pin_p_364.html
 * + http://www.varitube.com/VT-510--Bottom-Feeding-510--Self-Adjusting-or-Manual_p_402.html
 */

//$fn = 36;

//connector510("varitube12mmBottomFeed");
//connector510("varitube14mm");
//connector510("varitube22mmBottomFeed");
//connector510("varitube22mmV2Slotted");
//connector510("varitube22mmV2Plain");
//connector510("varitube28.5mm");

//connector510("fdv15.5mmV4");
//connector510("fdv22mmV4");
//connector510("fdv30mmV4");

//connector510("fdv15.5mmV4lp");
//connector510("fdv22mmV4lp");

//connector510("fdv22mmV4lps");

module connector510(type, color510="silver", holes=true) {
  if(type == "varitube12mmBottomFeed")
    varitube12mmBottomFeed(color510, holes);

  if(type == "varitube14mm")
    varitube14mm(color510, holes);

  if(type == "varitube22mmBottomFeed")
    varitube22mmBottomFeed(color510, holes);

  if(type == "varitube22mmV2Slotted")
    varitube22mmV2Slotted(color510, holes);

  if(type == "varitube22mmV2Plain")
    varitube22mmV2Flat(color510, holes);

  if(type == "varitube28.5mm")
    varitube285mm(color510, holes);

  if(type == "fdv15.5mmV4")
    fdv155mmV4(color510, holes);

  if(type == "fdv22mmV4")
    fdv22mmV4(color510, holes);

  if(type == "fdv30mmV4")
    fdv30mmV4(color510, holes);

  if(type == "fdv15.5mmV4lp")
    fdv155mmV4lp(color510, holes);

  if(type == "fdv22mmV4lp")
    fdv22mmV4lp(color510, holes);

  if(type == "fdv22mmV4lps")
    fdv22mmV4lps(color510, holes);
}

module fdv155mmV4(color510="silver", holes=true) {
  connectorDiameter = 8;
  connectorHeight = 2.5;
  bodyDiameter = 10;
  bodyHeight = 8;
  ringDiameter = 15.5;
  ringHeight = 3.5;

  fdvV4Base(
    connectorDiameter, connectorHeight,
    bodyDiameter, bodyHeight,
    ringDiameter, ringHeight,
    color510,
    holes, false
  );
}

module fdv22mmV4(color510="silver", holes=true) {
  connectorDiameter = 8;
  connectorHeight = 2.5;
  bodyDiameter = 10;
  bodyHeight = 8;
  ringDiameter = 22;
  ringHeight = 3.5;

  fdvV4Base(
    connectorDiameter, connectorHeight,
    bodyDiameter, bodyHeight,
    ringDiameter, ringHeight,
    color510,
    holes, false
  );
}

module fdv155mmV4lp(color510="silver", holes=true) {
  connectorDiameter = 8;
  connectorHeight = 2.5;
  bodyDiameter = 10;
  bodyHeight = 10.5;
  ringDiameter = 15.5;
  ringHeight = 1;

  fdvV4Base(
    connectorDiameter, connectorHeight,
    bodyDiameter, bodyHeight,
    ringDiameter, ringHeight,
    color510,
    holes, false
  );
}

module fdv22mmV4lp(color510="silver", holes=true) {
  connectorDiameter = 8;
  connectorHeight = 2.5;
  bodyDiameter = 10;
  bodyHeight = 10.5;
  ringDiameter = 22;
  ringHeight = 1;

  fdvV4Base(
    connectorDiameter, connectorHeight,
    bodyDiameter, bodyHeight,
    ringDiameter, ringHeight,
    color510,
    holes, false
  );
}

module fdv30mmV4(color510="silver", holes=true) {
  connectorDiameter = 8;
  connectorHeight = 2.5;
  bodyDiameter = 10;
  bodyHeight = 8;
  ringDiameter = 30;
  ringHeight = 3.5;

  fdvV4Base(
    connectorDiameter, connectorHeight,
    bodyDiameter, bodyHeight,
    ringDiameter, ringHeight,
    color510,
    holes, false
  );
}

module fdv22mmV4lps(color510="silver", holes=true) {
  connectorDiameter = 8;
  connectorHeight = 2.5;
  bodyDiameter = 10;
  bodyHeight = 9;
  ringDiameter = 22;
  ringHeight = 1;

  fdvV4Base(
    connectorDiameter, connectorHeight,
    bodyDiameter, bodyHeight,
    ringDiameter, ringHeight,
    color510,
    holes, false
  );
}

module fdvV4Base(
  connectorDiameter, connectorHeight,
  bodyDiameter, bodyHeight,
  ringDiameter, ringHeight,
  ringColor,
  holes=true, slotted=true)
{
  connectorRadius = connectorDiameter / 2;
  bodyRadius = bodyDiameter / 2;
  ringRadius = ringDiameter / 2;

  connectorColor = (holes) ? "gold" : ringColor;
  bodyColor = (holes) ? "gray" : ringColor;

  translate([0, 0, -connectorHeight - bodyHeight])
    main(ringColor);

  module main(ringColor) {
    group() {
      color(connectorColor)
        difference() {
          cylinder(r=connectorRadius, h=connectorHeight);

          if(holes)
            translate([0, 0,- 1])
              cylinder(r=connectorRadius / 3, h=connectorHeight);
        }

      difference() {
        group() {
          translate([0, 0, connectorHeight])
            color(bodyColor)
              cylinder(r=bodyRadius, h=bodyHeight);

          translate([0, 0, connectorHeight + bodyHeight])
            color(ringColor)
              cylinder(r=ringRadius, h=ringHeight);
        }

        if(holes)
          translate([0, 0, connectorHeight + bodyHeight / 2])
            color(ringColor)
              cylinder(r=3.5, h= ringHeight+ bodyHeight);
      }
    }
  }
}

module varitube14mm(color510="silver", holes=true) {
  bodyDiameter = 10;
  bodyHeight = 10.52;

  ringDiameter = 14;
  ringHeight = 1;

  holeDiameter = 7;
  holeHeight = bodyHeight;

  color(color510)
    varitubeBase(
      bodyDiameter, bodyHeight,
      ringDiameter, ringHeight,
      holeDiameter, holeHeight,
      holes, true);
}

module varitube12mm(color510="silver", holes=true) {
  bodyDiameter = 10;
  bodyHeight = 12;

  ringDiameter = 12;
  ringHeight = 1;

  holeDiameter = 7;
  holeHeight = bodyHeight;

  color(color510)
    varitubeBase(
      bodyDiameter, bodyHeight,
      ringDiameter, ringHeight,
      holeDiameter, holeHeight,
      holes, false);
}

module varitube12mmBottomFeed(color510="silver", holes=true) {
  squonkHeight = 10;
  squonkDiameter = 3;
  squonkRadius = squonkDiameter / 2;

  color(color510)
    group() {
      varitube12mm(holes);

      translate([0, 0, -12 - squonkHeight])
        cylinder(r=squonkRadius, h=squonkHeight);
    }
}

module varitube22mmBottomFeed(color510="silver", holes=true) {
  squonkHeight = 10;
  squonkDiameter = 3;
  squonkRadius = squonkDiameter / 2;

  color(color510)
    group() {
      varitube22mmV2(color510, holes, false);

      translate([0, 0, -10 - squonkHeight])
        cylinder(r=squonkRadius, h=squonkHeight);
    }
}

module varitube22mmV2Flat(color510="silver", holes=true) {
  varitube22mmV2(color510, holes, false);
}

module varitube22mmV2Slotted(color510="silver", holes=true) {
  varitube22mmV2(color510, holes, true);
}

module varitube22mmV2(color510="silver", holes=true, slotted=false) {
  bodyDiameter = 10;
  bodyHeight = 10.52;

  ringDiameter = 22;
  ringHeight = 1;

  holeDiameter = 7;
  holeHeight = bodyHeight;

  color(color510)
    varitubeBase(
      bodyDiameter, bodyHeight,
      ringDiameter, ringHeight,
      holeDiameter, holeHeight,
      holes, slotted);
}

module varitube285mm(color510="silver", holes=true) {
  bodyDiameter = 10;
  bodyHeight = 10.52;

  ringDiameter = 28.5;
  ringHeight = 1;

  holeDiameter = 7;
  holeHeight = bodyHeight;

  color(color510)
    varitubeBase(
      bodyDiameter, bodyHeight,
      ringDiameter, ringHeight,
      holeDiameter, holeHeight,
      holes, true);
}

module varitubeBase(
  bodyDiameter, bodyHeight,
  ringDiameter, ringHeight,
  holeDiameter, holeHeight,
  holes=true, slotted=true)
{
  bodyRadius = bodyDiameter / 2;
  ringRadius = ringDiameter / 2;
  holeRadius = holeDiameter / 2;

  union() {
    translate([0, 0, -bodyHeight])
      build();

    cube([0, 0, 0]);
  }

  module build() {
    difference() {
      union() {
        bottom();
        translate([0, 0, bodyHeight])
          top();
      }

      if(holes)
        translate([0, 0, ringHeight])
          cylinder(r=holeRadius, h=holeHeight + 1);
    }
  }

  module bottom() {
    cylinder(r=bodyRadius, h=bodyHeight + ringHeight / 2);
  }

  module top() {
    zOffset = ringHeight / 2;

    difference() {
      cylinder(r=ringRadius, h=ringHeight);

      if(holes && slotted) {
        translate([-ringDiameter / 2 - 1, 0, zOffset])
          cube([ringDiameter + 2, ringHeight, ringHeight]);

        translate([0, -ringDiameter / 2 - 1, zOffset])
          rotate([0, 0, 90])
            cube([ringDiameter + 2, ringHeight, ringHeight]);
      }
    }
  }
}/**
 * Available Bottles:
 * 
 * + Fat Daddy Vapes 18mm: http://www.modmaker.co.uk/Mod-Making-Supplies/Squonk-Parts/Fat-Daddy-Vapes-18mm-Squonk-Bottles
 */

//$fn = 36;
//bottle("fdv18mm", "silver", true);

module bottle(type, capColor="silver", holes=true) {
  if(type == "fdv18mm")
    fdv18mm(capColor, holes);
}

module fdv18mm(capColor="silver", holes=true) {
  diameter = 18;
  height = 65;
  capHeight = 15;

  radius = diameter / 2;

  if(holes)
    %build();
  
  else
    color(capColor)
      build();
    
  module build() {
    group() {
      cylinder(r=radius, h=height-capHeight - 10);
      translate([0, 0, height-capHeight - 10])
        cylinder(r=radius - 2, h=2);
      translate([0, 0, height-capHeight - 8])
        cylinder(r=radius, h=2);
      translate([0, 0, height-capHeight - 6])
        cylinder(r=radius - 2, h=2);
      translate([0, 0, height-capHeight - 4])
        cylinder(r=radius, h=2);
      translate([0, 0, height-capHeight - 2])
        cylinder(r=radius - 2, h=2);
    }
    
    translate([0,0, height- capHeight])
    color(capColor) {
      cylinder(r=radius, h=capHeight);
      
      translate([0,0, capHeight])
        cylinder(r=1.5, h=8);
    }
  }
}//elliptic(25, 15, 5);

module elliptic(height, width, depth) {
  radius = width / 2;

  translate([0, depth / 2, radius - height / 2])
    rotate([90, 0, 0])
      hull() {
        cylinder(r=radius, h = depth);

        translate([0, height - radius * 2], 0)
          cylinder(r=radius, h = depth);
      }
}/**
 * Available enclosures:
 *
 * Hammond:
 * + http://www.hammondmfg.com/pdf/1550A.pdf
 * + http://www.hammondmfg.com/pdf/1550B.pdf
 * + http://www.hammondmfg.com/pdf/1550P.pdf
 * + http://www.hammondmfg.com/pdf/1550Q.pdf
 *
 * + http://www.hammondmfg.com/pdf/1590A.pdf
 * + http://www.hammondmfg.com/pdf/1590B.pdf
 * + http://www.hammondmfg.com/pdf/1590G.pdf
 */

/* [Type] */
// Which part of the enclosure to view?
part = "both"; // [both, body, lid, test]

// Which type of box do you want?
type = "hammond1550a"; // [hammond1550a, hammond1550b, hammond1550p, hammond1550q, hammond1590a, hammond1590b, hammond1590g]

/* [Body] */

// Adjust in case your magnets do not fit width wise.
diameterOffsetBodyHole = -0.25; // [-5:0.05:5]

// Increase in case your magnet holders are too flimsy.
additionalWallBody = 0.00; // [-5:0.05:5]

// Decrease in case your magnets do not fit height wise.
zOffsetBodyHole = 0.00; // [-5:0.05:5]

/* [Lid] */

// Adjust in case your magnets do not fit width wise
diameterOffsetLidHole = 0.10; // [-5:0.05:5]

// Increase in case your magnet holder walls are too flimsy
additionalWallLid = 0.00; // [-5:0.05:5]

// Decrease in case your magnets do not fit height wise.
zOffsetLidHole = 0.00; // [-5:0.05:5]

// The width of the snaps
snapWidth = 1.00; // [-5:0.05:5]

/* [Hidden] */
$fn = 36;
print = false;

//enclosure(type, part);
 
module enclosure(type, part, boxColor) {
  if(type == "hammond1550a")
    hammond1550a(part, boxColor);

  if(type == "hammond1550b")
    hammond1550b(part, boxColor);

  if(type == "hammond1550p")
    hammond1550p(part, boxColor);

  if(type == "hammond1550q")
    hammond1550q(part, boxColor);

  if(type == "hammond1590a")
    hammond1590a(part, boxColor);

  if(type == "hammond1590b")
    hammond1590b(part, boxColor);

  if(type == "hammond1590g")
    hammond1590g(part, boxColor);
}

module hammond1550a(part="both", boxColor="grey") {
  outerLength = 89;
  outerWidth = 35;
  outerHeight = 30.1;
  lidHeight = 4.1;

  innerLength = 86;
  innerWidth = 32;
  innerHeight = 27.1;

  postDiameter = 4;
  postWidthBody = 6.5;
  postWidthLid = 6.5;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond1550b(part="both", boxColor="grey") {
  outerLength = 114.50;
  outerWidth = 64;
  outerHeight = 30.1;
  lidHeight = 4.1;

  innerLength = 111.50;
  innerWidth = 60.58;
  innerHeight = 27.1;

  postDiameter = 4;
  postWidthBody = 6.5;
  postWidthLid = 6.5;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond1550p(part="both", boxColor="grey") {
  outerLength = 80;
  outerWidth = 55;
  outerHeight = 25.1;
  lidHeight = 4.1;

  innerLength = 76.13;
  innerWidth = 52;
  innerHeight = 22.1;

  postDiameter = 4;
  postWidthBody = 6.5;
  postWidthLid = 6.5;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond1550q(part="both", boxColor="grey") {
  outerLength = 60;
  outerWidth = 55;
  outerHeight = 30.1;
  lidHeight = 4.1;

  innerLength = 57;
  innerWidth = 51;
  innerHeight = 26.8;

  postDiameter = 4;
  postWidthBody = 6.5;
  postWidthLid = 6.5;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond1590a(part="both", boxColor="grey") {
  outerLength = 92.6;
  outerWidth = 38.5;
  outerHeight = 31;
  lidHeight = 4.1;

  innerLength = 89.10;
  innerWidth = 35;
  innerHeight = 27.4;

  postDiameter = 4;
  postWidthBody = 6.5;
  postWidthLid = 6.5;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond1590b(part="both", boxColor="grey") {
  outerLength = 112.4;
  outerWidth = 60.5;
  outerHeight = 31;
  lidHeight = 4.1;

  innerLength = 108.1;
  innerWidth = 56.5;
  innerHeight = 27;

  postDiameter = 4;
  postWidthBody = 6;
  postWidthLid = 6;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond1590g(part="both", boxColor="grey") {
  outerLength = 100;
  outerWidth = 50;
  outerHeight = 25;
  lidHeight = 4.1;

  innerLength = 96.2;
  innerWidth = 46.2;
  innerHeight = 21;

  postDiameter = 4;
  postWidthBody = 6.5;
  postWidthLid = 6.5;
  holeDiameterBody = 3.5;
  holeDiameterLid = 4;

  xOffsetHole = 5;
  yOffsetHole = 5;

  outerDiameter = 7;

  hammond(
    outerLength, outerWidth, outerHeight, lidHeight,
    innerLength, innerWidth, innerHeight,
    postWidthBody, holeDiameterBody,
    postWidthLid, holeDiameterLid,
    xOffsetHole, yOffsetHole,
    outerDiameter, postDiameter,
    part, boxColor);
}

module hammond(
  outerLength, outerWidth, outerHeight, lidHeight,
  innerLength, innerWidth, innerHeight,
  postWidthBody, holeDiameterBody,
  postWidthLid, holeDiameterLid,
  xOffsetHole, yOffsetHole,
  outerDiameter, postDiameter,
  part, boxColor)
{
  holeRadiusBody = holeDiameterBody / 2;
  holeRadiusLid = holeDiameterLid / 2;
  outerRadius = outerDiameter / 2;
  postRadius = postDiameter / 2;
  bodyHeight = outerHeight - lidHeight;

  xOffset = (outerLength - innerLength) / 2;
  yOffset = (outerWidth - innerWidth) / 2;
  zOffset = (outerHeight - innerHeight) / 2;

  screwPosts = [
    [0, 0, zOffset],
    [outerLength, 0, zOffset],
    [outerLength, outerWidth, zOffset],
    [0, outerWidth, zOffset]
  ];

  translate([outerWidth / 2, -outerHeight / 2, outerLength]) {
    rotate([0,90,90]) {
      if(part == "both" && !print) {
        translate([outerLength, 0, outerHeight])
          rotate([0,180,0])
            color(boxColor)
              renderBody();

        %renderLid();
      }
      
      if(part == "both" && print) {
        yOffset = (outerLength + outerWidth) / 2;
        pos = [outerLength, yOffset, 0];
        rot = [90, 0, 270];

        translate(pos)
          rotate(rot){
            renderBody();
            
            translate([0, outerWidth +3, 0])
              renderLid();
          }
      }

      if(part == "body") {
        yOffset = (outerLength + outerWidth) / 2;
        pos = (print) ? [outerLength, yOffset, 0] : [outerLength, 0, outerHeight];
        rot = (print) ? [90, 0, 270] : [0,180,0];

        translate(pos)
          rotate(rot)
            color(boxColor)
              renderBody();
      }

      if(part == "lid") {
        yOffset = (outerLength + outerWidth) / 2;
        pos = (print) ? [outerLength, yOffset, 0] : [0, 0, 0];
        rot = (print) ? [90, 0, 270] : [0, 0, 0];

        translate(pos)
          rotate(rot)
            color(boxColor)
              renderLid();
      }

      if(part == "test") {
        length = 13;
        width = 13;
        height = 10;

        translate([outerLength - outerHeight, 0, outerLength/4])
        rotate([0, 90, 0]) {
          intersection() {
            translate([0, 0, outerHeight - height])
              cube([length, width, height]);

            translate([outerLength, 0, outerHeight])
              rotate([0,180,0])
                  renderBody();
          }

          translate([0, -outerWidth +  (length * 2), 0])
            intersection() {
              translate([0, outerWidth - width, outerHeight - height])
                cube([length, width, height]);
              
              translate([outerLength, 0, outerHeight])
                rotate([0,180,0])
                  renderLid();
            }
        }
      }
    }
  }
  
  module renderBody() {
    postHeight = bodyHeight - zOffset;
    holeRadius = holeRadiusBody + diameterOffsetBodyHole / 2;

    difference() {
      difference() {
        roundBody(bodyHeight);

        difference() {
          translate([xOffset, yOffset, zOffset])
            cube([innerLength, innerWidth, bodyHeight]);

          screwPosts(postWidthBody + additionalWallBody, postHeight);
        }
      }

      postHoles(outerHeight, holeRadius, zOffsetBodyHole);
    }
  }
  
  module renderLid() {
    zOffsetPost = 0.1;
    postHeight = lidHeight - zOffset + zOffsetPost;

    holeHeight = 3;
    holeRadius = holeRadiusLid + diameterOffsetLidHole / 2;
    zOffsetHole = -zOffset + lidHeight - holeHeight + zOffsetLidHole;
    
    difference() {
      group() {
        difference() {
          roundBody(lidHeight);

          translate([xOffset, yOffset, zOffset])
            cube([innerLength, innerWidth, lidHeight]);
        }

        translate([0, 0, -zOffsetPost])
          screwPosts(postWidthLid + additionalWallLid, postHeight, holeRadiusLid);
        
        translate([outerLength / 2 - 3.5, yOffset, zOffset])
          cube([7, snapWidth, lidHeight - zOffset + 1]);
        
        translate([outerLength / 2 - 3.5, outerWidth - yOffset - snapWidth, zOffset])
          cube([7, snapWidth, lidHeight - zOffset + 1]);
        
        translate([yOffset, outerWidth / 2 - 3.5, zOffset])
          cube([snapWidth, 7, lidHeight - zOffset + 1]);
        
        translate([outerLength - yOffset - snapWidth, outerWidth / 2 - 3.5, zOffset])
          cube([snapWidth, 7, lidHeight - zOffset + 1]);
      }

      postHoles(outerHeight, holeRadius, zOffsetHole);
    }
  }

  module roundBody(cutHeight) {
    cornersBottom = [
      [outerRadius, outerRadius, outerRadius],
      [outerLength - outerRadius, outerRadius, outerRadius],
      [outerLength - outerRadius, outerWidth - outerRadius, outerRadius],
      [outerRadius, outerWidth - outerRadius, outerRadius]
    ];

    cornersTop = [
      [outerRadius, outerRadius, outerRadius],
      [outerLength - outerRadius, outerRadius, outerRadius],
      [outerLength - outerRadius, outerWidth - outerRadius, outerRadius],
      [outerRadius, outerWidth - outerRadius, outerRadius]
    ];

    bodyInnerWidth = outerWidth - outerRadius * 2;
    bodyInnerLength = outerLength - outerRadius * 2;
    bodyInnerHeight = cutHeight- outerRadius;

    translate([0, outerRadius,outerRadius])
      cube([outerLength, bodyInnerWidth, bodyInnerHeight]);
    
    translate([outerRadius, 0, outerRadius])
      cube([bodyInnerLength, outerWidth,  bodyInnerHeight]);

    for(pos = cornersTop)
      translate(pos)
        cylinder(r=outerRadius, h = bodyInnerHeight);

    //1832 // 7170
    difference() {
      hull() {
        for(pos = cornersBottom)
          translate(pos)
            sphere(r=outerRadius);
      }

      translate([-1, -1, cutHeight])
        cube([outerLength + 2, outerWidth + 2, cutHeight]);
    }
  }

  module postHoles(height, radius, zOffset) {
    for(i = [0:3])
      translate(screwPosts[i])
        rotate([0, 0, 90 * i])
          translate([xOffsetHole, yOffsetHole, zOffset])
            cylinder(r=radius, h=height);
  }

  module screwPosts(postWidth, height, holeRadius) {
    for(i = [0:3])
      translate(screwPosts[i])
        rotate([0, 0, 90 * i])
          screwPost(postWidth, height, holeRadius);
  }

  module screwPost(postWidth, height) {
    offsetRadius = postWidth - postRadius;

    translate([min(xOffset, yOffset), min(xOffset, yOffset), 0]) {
      cube([postWidth, postWidth - postRadius, height]);

      cube([postWidth - postRadius, postWidth , height]);

      translate([offsetRadius, offsetRadius, 0])
        cylinder(r=postRadius, h=height);
    }
  }
}/**
 * Available displays:
 * 
 * Seed Technology:
 * + http://media.digikey.com/pdf/Data%20Sheets/Seeed%20Technology/114990163_Web.pdf
 */

/* [Settings] */

// Height of the border
height = 4;

// Wall thickness border top and bottom.
walls = 1.5;
// Wall thickness border left and right.
wallsSide = 5;
// Wall thickness lid.
wallsLid = 1;

// Tolerance for the display.
spacing = 0.25;
// Tolerance for the lid.
spacingLid = 0.025;

/* [Display measurements] */

// Display length
displayLength = 22.6;
// Display width.
displayWidth = 10;
// Display height without the board.
displayHeight = 6.3;

// Board length.
boardLength = 30;
// Board width.
boardWidth = 11;
// Board height including components on the back.
boardHeight = 2.5;

/* [Hidden] */

//$fn = 360;

screwHoleDiameter = 2;

segmentLength = 7;
paddingSegment = 2.2;

//seedVoltMeter(displayColor, "green", holes);
//seedVoltMeter(displayColor, "yellow", holes);
//seedVoltMeter(displayColor, "red", holes);
//seedVoltMeter(displayColor, "blue", holes);
//holder(height);

module holder(height) {
  heightLid = wallsLid + height + boardHeight + 1;

  outerLength = displayLength + (wallsSide + spacing ) * 2;
  outerWidth = displayWidth + (walls + spacing ) * 2;
  
  innerLength = displayLength + spacing * 2;
  innerWidth = displayWidth + spacing * 2;

  outerLengthTop = outerLength + (wallsLid + spacing + spacingLid ) * 2;
  outerWidthTop = outerWidth + (wallsLid + spacing + spacingLid) * 2;
  innerLengthTop = outerLengthTop - wallsLid * 2;
  innerWidthTop = outerWidthTop - wallsLid * 2;

  outletWidth = 4;
  xOffsetOutlet = outerLength + spacing + walls - outletWidth - 4.75;

  module bottom() {
    difference() {
      cube([outerLength, outerWidth, height]);

      translate([wallsSide, walls, -1])
        cube([innerLength, innerWidth, height + 2]);
    }
  }

  module top() {
    difference() {
      cube([outerLengthTop, outerWidthTop, heightLid]);

      translate([wallsLid, wallsLid, wallsLid])
        cube([innerLengthTop, innerWidthTop, heightLid  + 1]);

      translate([xOffsetOutlet, -1, wallsLid])
        cube([outletWidth, walls + 2, heightLid + 1]);
    }
  }

  translate([-outerLengthTop / 2, -outerWidthTop - 1, 0])
    top();

  translate([-outerLengthTop / 2, 1, 0])
    bottom();
}

module seedVoltMeter(displayColor="white", ledColor="green", holes=true) {
  screwHoleRadius = screwHoleDiameter /2 ;
  padding = (boardLength - displayLength) / 2;

  segmentWidth = (displayLength - paddingSegment * 4) / 3;

  segments = [
    [padding + 1.5, 1.5, boardHeight],
    [padding + segmentWidth + paddingSegment * 2, 1.5, boardHeight],
    [padding + (segmentWidth + paddingSegment) * 2 + paddingSegment, 1.5, boardHeight]
  ];

  translate([-boardLength / 2, -displayWidth / 2, 0]) {
    color("green") {
      difference() {
        cube([boardLength, boardWidth, boardHeight]);
        
        width = boardWidth / 3 + 1;
        length = (boardLength - displayLength) / 2 + 1;

        translate([-1, -1, -1])
          cube([length, width, boardHeight + 2]);

        translate([boardLength - length + 1, -1, -1])
          cube([length, width, boardHeight + 2]);
        
        translate([boardLength - length + 1, boardWidth -width + 1, -1])
          cube([length, width, boardHeight + 2]);
        
        translate([-1, boardWidth -width + 1, -1])
          cube([length, width, boardHeight + 2]);
        
        translate([length / 2 - 0.5, boardWidth / 2, -1])
          cylinder(r=screwHoleRadius, h = boardHeight + 2);
        translate([boardLength -length / 2 + 0.5, boardWidth / 2, -1])
          cylinder(r=screwHoleRadius, h = boardHeight + 2);
      }
    }
    color(displayColor)
      group() {
        difference() {
          translate([padding, 0, boardHeight])
            cube([displayLength, displayWidth, displayHeight]);

          if(holes)
            for(pos=segments)
              translate(pos)
                segment(displayHeight + 1);
        }
      }

      if(holes)
        for(pos=segments)
          translate(pos)
            color(ledColor)
              segment(displayHeight);
  }

  module segment(height, color="white") {
    length = segmentWidth - 2;
    width = 0.5;
    horizontal = [
      [1, 0, 0],
      [1, (segmentLength - width) / 2, 0],
      [1, segmentLength - width, 0]
    ];

    vertical = [
      [0, width, 0],
      [segmentWidth - width, width, 0],
      [0, width + (segmentLength - width * 3) / 2 +width, 0],
      [segmentWidth - width, width + (segmentLength - width * 3) / 2 +width, 0]
    ];

    group() {
      for(pos=horizontal)
        translate(pos)
          cube([length, width, height]);

      for(pos=vertical)
        translate(pos)
          cube([width, (segmentLength - width * 3) / 2, height]);

      translate([segmentWidth + width * 1.5, 0, 0])
        cube([width, width, height]);
    }
  }
}

//display("seedVoltMeterGreen");
//display("seedVoltMeterYellow");
//display("seedVoltMeterRed");
//display("seedVoltMeterBlue");

module display(type, displayColor="white", holes=true) {
  if(type == "seedVoltMeterGreen")
    seedVoltMeter(displayColor, "green", holes);

  if(type == "seedVoltMeterYellow")
    seedVoltMeter(displayColor, "yellow", holes);

  if(type == "seedVoltMeterRed")
    seedVoltMeter(displayColor, "red", holes);

  if(type == "seedVoltMeterBlue")
    seedVoltMeter(displayColor, "blue", holes);
}
/**
 * Available switches:
 * 
 * CloneTec:
 * + http://www.modparts.co.uk/switches/317-12mm-clonetec-switch-raised-stainless-steel-0638183554302.html
 * + http://www.modparts.co.uk/switches/313-12mm-clonetec-switch-flat-stainless-steel-0638183554272.html
 *
 * Generic 16mm Vandal Proof:
 * + http://www.modparts.co.uk/switches/156-16mm-stainless-steel-vandal-proof-switch-with-button-actuator-0638183551660.html
 * + http://www.modparts.co.uk/switches/158-16mm-stainless-steel-vandal-proof-switch-with-flat-actuator-0638183551684.html
 *
 * Generic 19mm Vandal Proof:
 * + http://www.modparts.co.uk/switches/159-19mm-stainless-steel-vandal-proof-switch-with-raised-button-actuator-0638183551691.html
 * + http://www.modparts.co.uk/switches/161-19mm-stainless-steel-vandal-proof-switch-with-flat-actuator-0638183551714.html 
 */
 
//switch("cloneTec12mmFlat");
//switch("cloneTec12mmRaised");
//switch("vandal16mmFlat");
//switch("vandal16mmRaised");
//switch("vandal19mmFlat");
//switch("vandal19mmRaised");

module switch(type, switchColor="silver", holes=true) {
    if(type == "cloneTec12mmFlat")
        cloneTec12mmFlat(switchColor, holes);

    if(type == "cloneTec12mmRaised")
        cloneTec12mmRaised(switchColor, holes);

    if(type == "vandal16mmFlat")
        vandal16mmFlat(switchColor, holes);

    if(type == "vandal16mmRaised")
        vandal16mmRaised(switchColor, holes);

    if(type == "vandal19mmFlat")
        vandal19mmFlat(switchColor, holes);

    if(type == "vandal19mmRaised")
        vandal19mmRaised(switchColor, holes);

}

module cloneTec12mmFlat(switchColor="silver", holes=true) {
    cloneTec12mm(switchColor, holes, false);
}

module cloneTec12mmRaised(switchColor="silver", holes=true) {
    cloneTec12mm(switchColor, holes, true);
}

module vandal16mmFlat(switchColor="silver", holes=true) {
    vandal16mm(switchColor, holes, false);
}

module vandal16mmRaised(switchColor="silver", holes=true) {
    vandal16mm(switchColor, holes, true);
}

module vandal19mmFlat(switchColor="silver", holes=true) {
    vandal19mm(switchColor, holes, false);
}

module vandal19mmRaised(switchColor="silver", holes=true) {
    vandal19mm(switchColor, holes, true);
}


module cloneTec12mm(switchColor="silver", holes=true, raised=false) {
  diameterBottom = 12.1;
  radiusBottom = diameterBottom / 2;
  heightBottom = 7.9;

  diameterRing = 14;
  radiusRing = diameterRing / 2;
  heightRing = 1.6;

  diameterButton = 8.6;
  radiusButton = diameterButton / 2;
  heightButton = (raised) ? 1.2 : 0;

  translate([0, 0, -heightBottom])
    color(switchColor)
      switchBase(
        radiusBottom, heightBottom,
        radiusRing, heightRing,
        radiusButton, heightButton);
}

module vandal16mm(switchColor="silver", holes=true, raised=false) {
  diameterBottom = 16;
  radiusBottom = diameterBottom / 2;
  heightBottom = 17;

  diameterRing = 18;
  radiusRing = diameterRing / 2;
  heightRing = 1.5;

  diameterButton = 11.7;
  radiusButton = diameterButton / 2;
  heightButton = (raised) ? 1.2 : 0;

  translate([0, 0, -heightBottom])
    color(switchColor)
      switchBase(
        radiusBottom, heightBottom,
        radiusRing, heightRing,
        radiusButton, heightButton);
}

module vandal19mm(switchColor="silver", holes=true, raised=false) {
  diameterBottom = 19;
  radiusBottom = diameterBottom / 2;
  heightBottom = 22.2;

  diameterRing = 22;
  radiusRing = diameterRing / 2;
  heightRing = 1.6;

  diameterButton = 15;
  radiusButton = diameterButton / 2;
  heightButton = (raised) ? 1.2 : 0;

  translate([0, 0, -heightBottom])
    color(switchColor)
      switchBase(
        radiusBottom, heightBottom,
        radiusRing, heightRing,
        radiusButton, heightButton);
}

module switchBase(
    radiusBottom, heightBottom,
    radiusRing, heightRing,
    radiusButton, heightButton)
{
  group() {
    cylinder(r=radiusBottom, h=heightBottom + 1);

    translate([0, 0, heightBottom])
      hull() {
        cylinder(r=radiusRing, h=heightRing / 2);

        translate([0, 0, heightRing / 2])
          cylinder(r=radiusButton + 0.5, h=heightRing / 2);
      }

    translate([0, 0, heightBottom + heightRing])
      cylinder(r=radiusButton, h=heightButton);
  }
}/**
 * Available displays:
 * 
 * Keystone 18650:
 * + http://keyelco.com/userAssets/file/M65p27.pdf
 */
//$fn = 360;

//sled("keystone1042");
//sled("keystone1042empty");
//sled("keystone1048");
//sled("keystone1048empty");
//sled("keystone1048left");
//sled("keystone1048right");

module sled(type, sledColor="black", holes=true) {
  if(type == "keystone1042")
    keystone1042(sledColor, holes);
  
  if(type == "keystone1042empty")
    keystone1042(sledColor, holes, true);

  if(type == "keystone1048")
      keystone1048(sledColor, holes);

  if(type == "keystone1048empty")
      keystone1048(sledColor, holes, "none");

  if(type == "keystone1048left")
      keystone1048(sledColor, holes, "left");

  if(type == "keystone1048right")
      keystone1048(sledColor, holes, "right");
}

module cell(radius, length) {
  cylinder(r=radius, h=length);
}

module keystone1042(sledColor="black", holes=true, empty=false) {
  outerLength = 77.1;
  outerWidth = 20.7;
  outerHeight = 16;

  cellLength = 68.9;
  cellDiameter = 18;
  cellRadius = cellDiameter / 2;

  xOffset = (outerLength - cellLength) / 2;
  yOffset = (outerWidth - cellDiameter) / 2 + cellRadius;
  zOffset = cellRadius;

  translate([-outerLength / 2, -outerWidth / 2, 0])
    group() {
      color(sledColor) difference() {
        hull() {
          cube([outerLength, outerWidth, outerHeight / 2]);
          translate([0, 3.5, outerHeight / 2])
            cube([outerLength, outerWidth - 7, outerHeight / 2]);
        }

        translate([xOffset, yOffset, zOffset])
          rotate([0, 90, 0])
            cell(cellRadius, cellLength);

        translate([xOffset, -1, outerHeight / 2])
          cube([10, outerWidth + 2, outerHeight]);

        translate([outerLength / 2 - 15, -1, outerHeight / 2])
          cube([30, outerWidth + 2, outerHeight]);

        translate([outerLength - xOffset - 10, -1, outerHeight / 2])
          cube([10, outerWidth + 2, outerHeight]);
    }

    if(!empty)
      translate([xOffset, yOffset, zOffset])
        rotate([0, 90, 0])
          color("green") cell(cellRadius, cellLength);
  }
}

module keystone1048(sledColor="black", holes=true, cells="both") {
  outerLength = 77.1;
  outerWidth = 39.8;
  outerHeight = 16;

  cellLength = 68.9;
  cellDiameter = 18;
  cellRadius = cellDiameter / 2;

  xOffset = (outerLength - cellLength) / 2;
  yOffset = (outerWidth - cellRadius * 2) / 2;
  zOffset = cellRadius;

  translate([-outerLength / 2, -outerWidth / 2, 0])
  group() {
    color(sledColor)
      difference() {
        hull() {
          cube([outerLength, outerWidth, outerHeight / 2]);
          translate([0, 3.5, outerHeight / 2])
            cube([outerLength, outerWidth - 7, outerHeight / 2]);
        }

        translate([xOffset, yOffset, zOffset])
          rotate([0, 90, 0])
            cell(cellRadius, cellLength);

        translate([xOffset, yOffset + cellRadius * 2, zOffset])
          rotate([0, 90, 0])
            cell(cellRadius, cellLength);

        translate([xOffset, -1, outerHeight / 2])
          cube([10, outerWidth + 2, outerHeight]);

        translate([xOffset, outerWidth/2 - cellRadius, outerHeight / 2])
          cube([cellLength, cellDiameter, outerHeight]);

        translate([outerLength / 2 - 15, -1, outerHeight / 2])
          cube([30, outerWidth + 2, outerHeight]);

        translate([outerLength - xOffset - 10, -1, outerHeight / 2])
          cube([10, outerWidth + 2, outerHeight]);
      }

      if(cells == "both" || cells == "left")
        translate([xOffset, yOffset, zOffset])
          rotate([0, 90, 0])
            color("green") cell(cellRadius, cellLength);

      if(cells == "both" || cells == "right")
      translate([xOffset, yOffset + cellRadius * 2, zOffset])
        rotate([0, 90, 0])
          color("green") cell(cellRadius, cellLength);
        
    }
}/**
 * Available PCB's:
 * 
 * + Naos Raptor 10A: https://media.digikey.com/pdf/Data%20Sheets/Lineage%20Power/Naos_Raptor_10A.pdf
 * + Naos Raptor 10A: http://apps.geindustrial.com/publibrary/checkout/NSR020A0X?TNR=Data%20Sheets|NSR020A0X|generic
 */

//pcb("raptor10a");
//pcb("raptor20a");

module pcb(type, holes=true) {
  if(type == "raptor10a")
    raptor10a(holes);

  if(type == "raptor20a")
      raptor20a(holes);
}

module raptor20a() {
  length= 36.8;
  width = 15.5;
  height = 9.2;

  pcbHeight = 1.5;
  componentHeight = height - pcbHeight;

  translate([-length / 2, -width / 2, 0])
    group() {
      color("green")
        cube([length, width, pcbHeight]);

      translate([length -15 -3, width - 11.5, pcbHeight])
        color("black")
          cube([15, 10, componentHeight]);
    }
}

module raptor10a() {
  length= 10.4;
  width = 16.5;
  height = 6.84;
  
  pcbHeight = 1.5;
  componentHeight = height - pcbHeight;
  
  translate([-length / 2, -width / 2, 0])
    group() {
      color("green")
        cube([length, width, pcbHeight]);
      
      translate([0.5, width - length + 0.5  , pcbHeight])
        color("black")
          cube([length - 1, length - 1, componentHeight]);
      
      translate([0.5, 3, pcbHeight])
        color("brown")
          cube([(length) / 2 - 1, (length / 3) - 1, componentHeight / 2]);
      
      translate([length - (length / 3) - 1 , 3, pcbHeight])
        color("brown")
          cube([(length) / 2 - 1, (length / 3) - 1, componentHeight / 2]);
    }
}










module renderFull() {
  difference() {
    enclosure(boxType, part, boxColor);

    translate(pos510)
      rotate(rot510)
        connector510(type_510_connector, boxColor, false);

    if(enableFireSwitch == "yes")
      translate(posFireSwitch)
        rotate(rotFireSwitch)
          switch(fireSwitchType, boxColor, false);

    if(enableVoltMeter == "yes")
      translate(posVoltMeter)
        rotate(rotVoltMeter)
          display(voltMeterType, boxColor, false);

    if(enableSled == "yes")
      translate(posSled)
        rotate(rotSled)
          sled(sledType, boxColor, false);

    if(enablePcb == "yes")
      translate(posPcb)
        rotate(rotPcb)
          pcb(pcbType, false);

    if(enableSquonk == "yes") {
      translate(posSquonkCutout)
        rotate(rotSquonkCutout)
          color(boxColor)
            elliptic(squonkCutoutHeight, squonkCutoutWidth, squonkCutoutDepth);

      translate(posSquonk)
        rotate(rotSquonk)
            bottle(squonkType, boxColor, false);
    }
    
    if(enablePassthrough == "yes") {
      if(passthroughType == "hole")
        translate(posPassthrough)
          rotate(rotPassthrough)
            color(boxColor)
              elliptic(passthroughDiameter / 2, passthroughDiameter / 2, passthroughDepth);
      else
        translate(posPassthrough)
          rotate(rotPassthrough)
            passthrough(passthroughType, boxColor, false);
    }
  }
}

module visualisation() {
  translate(pos510)
    rotate(rot510)
      connector510(type_510_connector, color_510_connector);

  if(enableFireSwitch == "yes")
    translate(posFireSwitch)
      rotate(rotFireSwitch)
        switch(fireSwitchType, fireSwitchColor);

  if(enableVoltMeter == "yes")
    translate(posVoltMeter)
      rotate(rotVoltMeter)
        display(voltMeterType, voltMeterColor);

  if(enableSled == "yes")
    translate(posSled)
      rotate(rotSled)
        sled(sledType, sledColor);

  if(enablePcb == "yes")
    translate(posPcb)
      rotate(rotPcb)
        pcb(pcbType);

  if(enableSquonk == "yes") {
    translate(posSquonkCutout)
      rotate(rotSquonkCutout)
        #elliptic(squonkCutoutHeight, squonkCutoutWidth, squonkCutoutDepth);

    translate(posSquonk)
      rotate(rotSquonk)
        bottle(squonkType);
  }
  
  if(enablePassthrough == "yes") {
      if(passthroughType == "hole")
        translate(posPassthrough)
          rotate(rotPassthrough)
            #elliptic(passthroughDiameter / 2, passthroughDiameter / 2, passthroughDepth);

      else
        translate(posPassthrough)
          rotate(rotPassthrough)
            passthrough(passthroughType);
    }
}